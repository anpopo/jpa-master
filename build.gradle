buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }

    repositories {
        mavenCentral()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }

    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.7.11'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.15.RELEASE'
        classpath 'gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "com.ewerk.gradle.plugins.querydsl"

    group 'org.example'
    version '1.0-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.projectlombok:lombok'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.security:spring-security-test'

        // queryDsl 의존성 추가
        implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
        implementation "com.querydsl:querydsl-apt:${queryDslVersion}"
    }

    test {
        useJUnitPlatform()
    }

    ext {
        set("springCloudVersion", "2021.0.3")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }


    // querydsl 사용할 경로 지정합니다. 현재 지정한 부분은 .gitignore에 포함되므로 git에 올라가지 않습니다.
    def querydslDir = "$buildDir/generated/'querydsl'"

    // JPA 사용여부 및 사용 경로 설정
    querydsl {
        jpa = true
        querydslSourcesDir = querydslDir
    }

    // build시 사용할 sourceSet 추가 설정
    sourceSets {
        main.java.srcDir querydslDir
    }

    // querydsl 컴파일 시 사용할 옵션 설정
    compileQuerydsl {
        options.annotationProcessorPath = configurations.querydsl
    }

    // querydsl이 compileClassPath를 상속하도록 설정
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        querydsl.extendsFrom compileClasspath
    }
}
